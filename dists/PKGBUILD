# Maintainer: eloaders <eloaders at linux dot pl>

pkgname=i-nex-git
pkgver=7.2.1.100.gb0b6923
pkgrel=1
pkgdesc="System information tool like hardinfo, sysinfo"
arch=('i686' 'x86_64')
url="http://i-nex.linux.pl/"
license=('LGPL3')
depends=('gambas3-runtime' 
	 'gambas3-gb-image' 
	 'gambas3-gb-form'
         'gambas3-gb-desktop'
         'gambas3-gb-qt4'
         'python2' 
         'libcpuid-git'
         'xorg-server-utils' 
         'lsb-release' 
         'curl')
makedepends=('gambas3-devel' 'gcc' 'imagemagick' 'git')
source=($pkgname::'git://github.com/eloaders/I-Nex.git')
sha256sums=('SKIP')
provides=('i-nex' 'i-nex-bzr' 'i-nex-dev')
conflicts=('i-nex' 'i-nex-bzr' 'i-nex-dev')
pkgver() {
  cd $pkgname
  git describe --tags | tr - .
}
prepare() {
  cd $pkgname
  sed -i 's|python3$|python2|' pastebinit
  # make it dynamic
  sed -i -e 's|^STATIC.*|STATIC = false|' i-nex.mk
  cd I-Nex
  autoreconf -i
}
 
build() {
  cd $pkgname
  cd I-Nex
  ./configure --prefix=/usr
  cd ..
  make
}
 
package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}